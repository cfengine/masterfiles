bundle agent host_info_report
# @brief Generates a short high level summary of the executing host information
#
# **Example:**
# `cf-agent -b host_info_report`
{
  classes:
    "have_$(cfengine_info_files)"
      expression => filesexist("$(sys.masterdir)/$(cfengine_info_files)"),
      handle => "host_info_report_classes_have_cfengien_info_file",
      comment => "We need to know if we have the files, because if we try to
                  read them when they don't exist we get error messages.";
  vars:

    any::
    "cfengine_info_files"
      handle => "host_info_report_vars_cfengine_info_files",
      slist => { "cf_promises_validated", "cf_promises_release_id" },
      comment => "These files are required for CFEngine related information,
		  and if we try to read them when they don't exist we get ugly
                  error messages";

    "host_info_report_template"
      string => "$(this.promise_dirname)/../templates/$(this.bundle).mustache",
      comment => "Where the report template is found";

    "host_info_report_txt"
      string => "$(sys.workdir)/reports/$(this.bundle).txt",
      comment => "Where the host info report should be generated";

    "installed_packages"
      data => packagesmatching(".*", ".*", ".*", ".*");

    have_cf_promises_validated::
    "cf_promises_validated"
      data => readjson("$(sys.masterdir)/cf_promises_validated", 1K);

    "cf_promises_validated_timestamp"
      string => "$(cf_promises_validated[timestamp])";

    "cf_promises_validated_timestamp_formatted"
      string => strftime("localtime", "%F %T %Z", $(cf_promises_validated_timestamp)),
      comment => "It's useful to know when policy was last updated and verified";

    have_cf_promises_release_id::
    "cf_promises_release_id"
      data => readjson("$(sys.inputdir)/cf_promises_release_id", 1K);

    "cf_promises_release_id_releaseId"
      string => "$(cf_promises_release_id[releaseId])";

    any::
    "last_agent_run"
      string => strftime("localtime", "%F %T %Z", filestat("$(sys.workdir)/outputs/previous", "mtime"));

    "printable"
      string => format("%S", installed_packages);

  files:
    "$(host_info_report_txt)"
      create => "true",
      edit_template => "$(host_info_report_template)",
      handle => "host_info_report_files_host_info_report_txt",
      classes => scoped_classes_generic("bundle", "host_info_report_txt"),
      template_method => "mustache";

  reports:
    host_info_report_txt_repaired::
      "Host info report generated and avilable at '$(host_info_report_txt)'";

    host_info_report_txt_not_ok::
      "There was a problem generating your host info report at '$(host_info_report_txt)'";
}
