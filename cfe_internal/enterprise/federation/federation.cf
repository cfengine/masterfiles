body file control
{
        inputs => { "$(sys.libdir)/stdlib.cf" };
        namespace => "cfengine_enterprise_federation";
}

bundle agent config
# @brief Read/parse config JSON, define variables and classes for use later
{
  vars:
    enterprise_edition.(policy_server|am_policy_hub)::
      "federation_dir" string => "/opt/cfengine/federation";
      "bin_dir" string => "$(federation_dir)/bin";
      "path" string => "$(federation_dir)/cfapache/federation-config.json";
  classes:
    enterprise_edition.(policy_server|am_policy_hub)::
      "enabled"
        expression => fileexists( $(path) ),
        scope => "namespace";
  vars:
    enabled::
      "data" data => readjson( $(path) );
      "remotes" slist => getindices( @(data[remote_hubs]) );

  classes:
    enabled::

      "am_feeder"
        and => {strcmp("superhub", "$(data[remote_hubs][$(remotes)][role])"),
                strcmp("true", "$(data[remote_hubs][$(remotes)][enabled])")},
        comment => "Has a remote hub with superhub role, should act as feeder",
        scope => "namespace";

      "am_superhub"
        and => {strcmp("feeder", "$(data[remote_hubs][$(remotes)][role])"),
                strcmp("true", "$(data[remote_hubs][$(remotes)][enabled])")},
        comment => "Has a remote hub with feeder role, should act as superhub",
        scope => "namespace";

      "am_transporter"
        and => {strcmp("superhub", "$(data[remote_hubs][$(remotes)][role])"),
                strcmp("true", "$(data[remote_hubs][$(remotes)][enabled])"),
                strcmp("push_over_rsync", "$(data[remote_hubs][$(remotes)][transport][mode])")},
        comment => "Has an enabled remote superhub with push as transport method, should run transport ",
        scope => "namespace";

  reports:
    enabled.(DEBUG|DEBUG_config)::
      "Federation enabled!";
    am_superhub.(DEBUG|DEBUG_config)::
      "I'm a superhub!";
    am_feeder.(DEBUG|DEBUG_config)::
      "I'm a feeder!";
    am_transporter.(DEBUG|DEBUG_config)::
      "I'm a transporter!";
}

bundle agent transport_user
# @brief Manage cfdrop user and permissions for remote SSH access
{
  vars:
      "user"
        string => "cfdrop";
      "home"
        string => "$(cfengine_enterprise_federation:config.federation_dir)/superhub/drop";
      "create_files"
        slist => {
          "$(home)/.",
          "$(home)/.ssh/.",
          "$(home)/.ssh/authorized_keys"
        };
  classes:
    enabled::
      "selinux_enabled"
        expression => strcmp("Enforcing", execresult("getenforce", useshell));
      "missing_ssh_context"
        expression => not(regcmp(".*[\s:]ssh_home_t[\s:].*",
                                 execresult("ls -Z $(home)/.ssh/authorized_keys",
                                            useshell)));
  users:
    "$(user)"
      policy => "present",
      home_dir => "$(home)";
  files:
    "$(create_files)"
      create => "true";
    "$(home)/."
      depth_search => default:recurse_with_base("inf"),
      file_select => default:all,
      perms => default:mog( "600", $(user), "root" );
  commands:
    selinux_enabled.missing_ssh_context::
      "restorecon -F $(home)/.ssh/authorized_keys"
        contain => default:in_shell;
}


bundle agent federation_manage_files
# @brief Manage files, directories and permissions in $(cfengine_enterprise_federation:config.federation_dir)
{
  files:
    enterprise_edition.(policy_server|am_policy_hub)::
      "$(cfengine_enterprise_federation:config.federation_dir)/."
        create => "true",
        perms => default:mog( "701", "root", "root" );
      "$(cfengine_enterprise_federation:config.federation_dir)/cfapache/."
        create => "true",
        perms => default:mog( "600", "cfapache", "root" );
      "$(cfengine_enterprise_federation:config.federation_dir)/cfapache/."
        depth_search => default:recurse_with_base("inf"),
        file_select => default:all,
        perms => default:mog( "600", "cfapache", "root" );
    enabled::
      "$(cfengine_enterprise_federation:config.bin_dir)/."
        create => "true",
        perms => default:mog( "600", "root", "root" );
    am_superhub::
      "$(cfengine_enterprise_federation:config.federation_dir)/superhub/."
        create => "true",
        perms => default:mog( "701", "root", "root" );
      "$(cfengine_enterprise_federation:config.federation_dir)/superhub/import/."
        create => "true",
        perms => default:mog( "600", "root", "root" );
      "$(cfengine_enterprise_federation:config.federation_dir)/superhub/import/filters/."
        create => "true",
        perms => default:mog( "600", "root", "root" );
    am_feeder::
      "$(cfengine_enterprise_federation:config.federation_dir)/fedhub/."
        create => "true",
        perms => default:mog( "600", "root", "root" );
      "$(cfengine_enterprise_federation:config.federation_dir)/fedhub/dump/."
        create => "true",
        perms => default:mog( "600", "root", "root" );
      "$(cfengine_enterprise_federation:config.federation_dir)/fedhub/transport/."
        create => "true",
        perms => default:mog( "600", "root", "root" );

    # TODO: Render these from templates:

    am_feeder|am_transporter|am_superhub::
      # TODO: Instrument augments
      "$(cfengine_enterprise_federation:config.bin_dir)/config.sh"
        create => "true",
        template_method => "mustache",
        edit_template => "$(this.promise_dirname)/../../../templates/federated_reporting/config.sh.mustache",
        perms => default:mog( "600", "root", "root" );

      # TODO: Instrument augments
      "$(cfengine_enterprise_federation:config.bin_dir)/log.sh"
        create => "true",
        template_method => "mustache",
        edit_template => "$(this.promise_dirname)/../../../templates/federated_reporting/log.sh.mustache",
        perms => default:mog( "600", "root", "root" );

      "$(cfengine_enterprise_federation:config.bin_dir)/parallel.sh"
        copy_from => default:local_dcp( "$(this.promise_dirname)/../../../templates/federated_reporting/parallel.sh" ),
        perms => default:mog( "600", "root", "root" );

    am_feeder::
      "$(cfengine_enterprise_federation:config.bin_dir)/dump.sh"
        copy_from => default:local_dcp( "$(this.promise_dirname)/../../../templates/federated_reporting/dump.sh" ),
        perms => default:mog( "700", "root", "root" );

    am_transporter::
      "$(cfengine_enterprise_federation:config.bin_dir)/transport.sh"
        copy_from => default:local_dcp( "$(this.promise_dirname)/../../../templates/federated_reporting/transport.sh" ),
        perms => default:mog( "700", "root", "root" );

    am_superhub::
      "$(cfengine_enterprise_federation:config.bin_dir)/import.sh"
        copy_from => default:local_dcp( "$(this.promise_dirname)/../../../templates/federated_reporting/import.sh" ),
        perms => default:mog( "700", "root", "root" );

      "$(cfengine_enterprise_federation:config.bin_dir)/import_file.sh"
        copy_from => default:local_dcp( "$(this.promise_dirname)/../../../templates/federated_reporting/import_file.sh" ),
        perms => default:mog( "700", "root", "root" );

      "$(cfengine_enterprise_federation:config.federation_dir)/superhub/import/filters/filter.sed"
        create => "true",
        template_method => "mustache",
        edit_template => "$(this.promise_dirname)/../../../templates/federated_reporting/filter.sed.mustache",
        perms => default:mog( "600", "root", "root" );


}

bundle agent postgres_config
# @brief Customize postgres config for superhub
{
  files:
    am_superhub::
      "/var/cfengine/state/pg/data/postgresql.conf"
        edit_line => default:insert_lines("shared_buffers=1GB");
      "/var/cfengine/state/pg/data/postgresql.conf"
        edit_line => default:insert_lines("max_locks_per_transaction=4000");
}

bundle agent exported_data
# @brief Run script to dump pg data on feeder hub
{
  commands:
    am_feeder::
      # TODO: pass correct arguments once script is done
      "bash"
        arglist => {"$(cfengine_enterprise_federation:config.bin_dir)/dump.sh",
                    "$(cfengine_enterprise_federation:config.federation_dir)/config.sh"},
        contain => default:in_shell;
}

bundle agent data_transport
# @brief Run script to transport data from feeder to superhub
{
  commands:
    am_transporter::
      # TODO: pass correct arguments once script is done
      "bash"
        arglist => {"$(cfengine_enterprise_federation:config.bin_dir)/transport.sh",
                    "$(cfengine_enterprise_federation:config.federation_dir)/config.sh"},
        contain => default:in_shell;
}

bundle agent imported_data
# @brief Run script to import dumps on superhub
{
  commands:
    am_superhub::
      # TODO: pass correct arguments once script is done
      "bash"
        arglist => {"$(cfengine_enterprise_federation:config.bin_dir)/import.sh",
                    "$(cfengine_enterprise_federation:config.federation_dir)/config.sh"},
        contain => default:in_shell;
}

bundle agent entry
# @brief Conditionally runs all federated reporting bundles
{
  meta:
    (policy_server|am_policy_hub).enterprise_edition::
      "tags" -> { "ENT-4383" }
        slist => { "enterprise_maintenance" };
  classes:
    enterprise_edition.(policy_server|am_policy_hub)::
      "config_exists"
        expression => fileexists("$(cfengine_enterprise_federation:config.federation_dir)/cfapache/federation-config.json");
  methods:
    config_exists::
      "CFEngine Enterprise Federation Configuration"
        usebundle => config;
    enterprise_edition.(policy_server|am_policy_hub)::
      "federation_manage_files"
        usebundle => federation_manage_files;
    am_superhub::
      "CFEngine Enterprise Federation Transport User"
        usebundle => transport_user;
    enabled::
      "CFEngine Enterprise Federation Postgres Configuration"
        usebundle => postgres_config;
      "CFEngine Enterprise Federation Feeder Data Export"
        usebundle => exported_data;
      "CFEngine Enterprise Federation Feeder Data Transport"
        usebundle => data_transport;
      "CFEngine Enterprise Federation Feeder Data Import"
        usebundle => imported_data;
  reports:
    !enterprise_edition::
      "Federated reporting is only available in CFEngine Enterprise.";
    enterprise_edition.!(policy_server|am_policy_hub)::
      "Federated reporting is only available on the policy server / hub.";
}

body file control
{
        namespace => "default";
}

bundle agent __main__
# @brief You can run this policy file from shell without specifying bundle
{
  methods:
    "entry" usebundle => cfengine_enterprise_federation:entry;
}
