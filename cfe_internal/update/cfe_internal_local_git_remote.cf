bundle common cfe_internal_local_git_remote_file_control
{
  vars:
      "inputs" slist => { "$(this.promise_dirname)/lib.cf" };
}

body file control
{
  inputs => { @(cfe_internal_local_git_remote_file_control.inputs) };
}

bundle agent cfe_internal_local_git_remote
{
  files:
      "$(update_def.local_masterfiles_git)/."
      handle => "cfe_internal_local_git_remote_files_have_local_git_remote_masterfiles_git_perms",
      perms => u_mog($(update_def.masterfiles_perms_mode),
                     $(update_def.cf_apache_user),
                     $(update_def.cf_apache_group)),
      depth_search => u_cfe_internal_recurse("inf"),
      comment => "Apache needs the ability to write to this repository in order to
                  support sketch activation deployments from Mission Portal",
      classes => u_if_else("did_perms_local_git_remote_clone",
                           "failed_perms_local_git_remote_clone");

  reports:
    inform_mode.did_perms_local_git_remote_clone::
      "$(this.bundle): set the permissions of the Git clone $(update_def.local_masterfiles_git)";
    inform_mode.failed_perms_local_git_remote_clone::
      "$(this.bundle): failed to set the permissions of the Git clone $(update_def.local_masterfiles_git)";
}
