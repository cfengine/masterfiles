body common control
{
      inputs => { '../../default.cf.sub' };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}
#######################################################

bundle common classes
{
  classes:

      "testing_masterfiles_policy_framework"
        expression => "any",
        scope => "namespace",
        comment => "This class is needed so that dcs.cf.sub includes the stdlib instead of using plucked.cf.sub from core which might get out of date and cause us to not test current code from the MPF.";
}

bundle agent init
{
  files:
      # The tested file "actual" is copied from our seeded starting position.
      "$(G.testfile)"
      copy_from => local_cp("$(this.promise_filename).start");

}

#######################################################

bundle agent test
{
  meta:
      "description" -> { "CFE-3483" }
        string => "Test that converge_prepend works as expected";

      "test_soft_fail" string => "cfengine_3_7",
        meta  => { "CFE-3483" };


  files:
      "$(G.testfile)"
        edit_line => converge_prepend( "covfefe", "Despite the constant negative press covfefe" );

}

#######################################################

bundle agent check
{
  methods:
      "Pass/FAIL"
        usebundle => dcs_check_diff($(G.testfile), "$(this.promise_filename).expected", $(this.promise_filename));
}
