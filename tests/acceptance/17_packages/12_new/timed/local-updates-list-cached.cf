# Test that the local updates list cache is called instead of the online
# version when ifelapsed has not expired.

body common control
{
    inputs => { "../../../default.cf.sub" };
    bundlesequence => { default($(this.promise_filename)) };
}

bundle agent init
{
  meta:
      "test_suppress_fail" string => "any",
        meta => { "redmine7144" };

  files:
    test_pass_1::
      "$(sys.workdir)/package_modules/test_module"
        copy_from => local_cp("$(this.promise_dirname)/test_module"),
        perms => m("0755");

  methods:
    test_pass_1::
      "any" usebundle => file_make($(G.testfile), "get-package-data
list-installed
repo-install
list-installed
list-updates
END-OF-AGENT-RUN---
get-package-data
repo-install
list-installed
list-updates-local");
}

bundle agent test
{
  commands:
      "$(sys.cf_agent) -D AUTO -f $(this.promise_filename).sub";

      "$(G.echo) END-OF-AGENT-RUN--- >> $(sys.workdir)/package_modules/test_module.log"
        contain => in_shell;
}

bundle agent check
{
  methods:
    test_pass_1::
      "any" usebundle => dcs_wait($(this.promise_filename), 65);
    test_pass_2::
      "any" usebundle => dcs_check_diff("$(sys.workdir)/package_modules/test_module.log",
                                        $(G.testfile), $(this.promise_filename));
}
