bundle common inventory_debian
# @brief Debian inventory
#
# This common bundle is for Debian inventory work.
{
  vars:
    has_os_release::
      "os_release_info" string => readfile("/etc/os-release","256"),
      comment => "Read more OS info" ;

    has_lsb_release::
     "lsb_release_info" string => readfile("/etc/lsb-release","256"),
      comment => "Read more OS info" ;

    has_etc_linuxmint_info::
      "linuxmint_info"  string => readfile("/etc/linuxmint/info","1024"),
      comment => "Read Linux Mint specific info" ;


  classes:
    any::
      "debian_derived_evaluated" or => { "has_os_release","has_lsb_release" } ;

    has_os_release::
      "linuxmint"
      expression => regcmp("(?ms).*^ID=linuxmint$.*", "$(os_release_info)"),
      comment => "this is a Linux Mint system, of some sort" ;

    has_lsb_release::
      "linuxmint"
      expression => regcmp("(?ms).*^DISTRIB_ID=LinuxMint$.*", "$(lsb_release_info)"),
      comment => "this is a Linux Mint system, of some sort" ;

    linuxmint.has_os_release::
      "lmde"
      expression => regcmp('(?ms).*^NAME="Linux Mint LMDE"$.*', "$(os_release_info)"),
      comment => "this is a Linux Mint Debian Edition" ;

    linuxmint.has_lsb_release::
      "lmde"
      expression => regcmp('(?ms).*^DISTRIB_DESCRIPTION="LMDE.*', "$(lsb_release_info)"),
      comment => "this is a Linux Mint Debian Edition" ;

    linuxmint.has_etc_linuxmint_info::
      "lmde"
      expression => regcmp('(?ms).*^DESCRIPTION="LMDE.*',"$(linuxmint_info)"),
      comment => "this is a Linux Mint Debian Edition" ;


    debian_derived_evaluated::
      "debian_pure" expression => "debian.!(ubuntu|linuxmint)",
      comment => "pure Debian",
      meta => { "inventory", "attribute_name=none" };

      "debian_derived" expression => "debian.!debian_pure",
      comment => "derived from Debian",
      meta => { "inventory", "attribute_name=none" };

    any::
      "has_os_release" expression => fileexists("/etc/os-release"),
      comment => "Check if we can get more info from /etc/os-release",
      meta => { "inventory", "attribute_name=none" };

      "has_lsb_release" expression => fileexists("/etc/lsb-release"),
      comment => "Check if we can get more info from /etc/lsb-release",
      meta => { "inventory", "attribute_name=none" };

      "has_etc_linuxmint_info" expression => fileexists("/etc/linuxmint/info"),
      comment => "If this is a Linux Mint system, this *could* be available",
      meta => { "inventory", "attribute_name=none" };
}
