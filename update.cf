###############################################################################
#
#   update.cf - Basic Update Policy
#
###############################################################################

body common control
{
      bundlesequence => {
                          "u_def",
                          "cfe_internal_dc_workflow",
                          "cfe_internal_update_policy",
                          "cfe_internal_update_processes",
                          "cfe_internal_update_bins"
      };

      version => "update.cf 3.6.0";

      inputs => {
                  "update/update_bins.cf",
                  "update/cfe_internal_dc_workflow.cf",
                  "update/cfe_internal_local_git_remote.cf",
                  "update/cfe_internal_update_from_repository.cf",
                  "update/update_policy.cf",
                  "update/update_processes.cf"
      };
}

#############################################################################

body agent control
{
      ifelapsed => "1";
      skipidentify => "true";
}

#############################################################################

bundle common u_def
{
  vars:
      "input_name_patterns" slist => { ".*.cf",".*.dat",".*.txt", ".*.conf", ".*.mustache",
                                       "cf_promises_release_id" },
      comment => "Filename patterns to match when updating the policy (see update/update_policy.cf)",
      handle => "common_def_vars_input_name_patterns";

      # the permissions for your masterfiles, which will propagate to inputs
      "masterfiles_perms_mode" string => "0600",
      handle => "common_def_vars_masterfiles_perms_mode";

      "dc_scripts" string => "/var/cfengine/httpd/htdocs/api/dc-scripts",
      comment => "Directory where design center scripts are located on Enterprise Hub";

      "DCWORKFLOW" string => "/opt/cfengine",
      comment => "Directory where DC Workflow related things are stored";

      "masterfiles_staging" string => "$(DCWORKFLOW)/masterfiles_staging",
      comment => "Directory where Mission Portal Design Center
                  activations are staged for commit to upstream repository";

      "local_masterfiles_git" string => "$(DCWORKFLOW)/masterfiles.git",
      comment => "Local bare git repository, serves as OOTB upstream repo";

      "cf_git" string => "$(sys.workdir)/bin/git",
      comment => "Path to git binary installed with CFEngine Enterprise";

    cfengine_3_4|cfengine_3_5::
      "cf_apache_user" string => "apache",
      handle => "common_u_def_vars_cf_apache_user",
      comment => "User that CFEngine Enterprise webserver runs as. Needs to stay in sync with def.cf_apache_user";

      "cf_apache_group" string => "apache",
      handle => "common_u_def_vars_cf_apache_group",
      comment => "Group that CFEngine Enterprise webserver runs as. Needs to stay in sync with def.cf_apache_group";

    !cfengine_3_4.!cfengine_3_5::
      "cf_apache_user" string => "cfapache",
      handle => "common_u_def_vars_cf_cfapache_user",
      comment => "User that CFEngine Enterprise webserver runs as. Needs to stay in sync with def.cf_apache_user";

      "cf_apache_group" string => "cfapache",
      handle => "common_u_def_vars_cf_cfapache_group",
      comment => "Group that CFEngine Enterprise webserver runs as. Needs to stay in sync with def.cf_apache_group";


  classes:
      # Transfer policies and binaries with encryption
      # you can also request it from the command line with
      # -Dcfengine_internal_encrypt_transfers
      "cfengine_internal_encrypt_transfers" expression => "!any";

      # Purge policies that don't exist on the server side.
      # you can also request it from the command line with
      # -Dcfengine_internal_purge_policies
      "cfengine_internal_purge_policies" expression => "!any";

      # Update masterfiles from Git
      # Enabled for enterprise users by default
      # you can also request it from the command line with
      # -Dcfengine_internal_masterfiles_update
      "cfengine_internal_masterfiles_update" expression => "enterprise.!(cfengine_3_4|cfengine_3_5)";

}
